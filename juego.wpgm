import wollok.game.*
import pokemon.*

const pikachu = new Pokemon(
    nombre = "Pikachu",
    vida = 100,
    vidaMaxima = 100,
    ataque = 25,
    defensa = 10,
    tipo = "Eléctrico",
    imagen = "pikachu.png",
    position = game.at(-10, -10)
)

const charmander = new Pokemon(
    nombre = "Charmander",
    vida = 120,
    vidaMaxima = 120,
    ataque = 30,
    defensa = 15,
    tipo = "Fuego",
    imagen = "charmander.png",
    position = game.at(-10, -10)
)

const squirtle = new Pokemon(
    nombre = "Squirtle",
    vida = 110,
    vidaMaxima = 110,
    ataque = 20,
    defensa = 20,
    tipo = "Agua",
    imagen = "squirtle.png",
    position = game.at(-10, -10)
)

const bulbasaur = new Pokemon(
    nombre = "Bulbasaur",
    vida = 115,
    vidaMaxima = 115,
    ataque = 22,
    defensa = 18,
    tipo = "Planta",
    imagen = "bulbasaur.png",
    position = game.at(-10, -10)
)

// POKÉMON ENEMIGOS
const rattata = new Pokemon(
    nombre = "Rattata",
    vida = 130,
    vidaMaxima = 130,
    ataque = 30,
    defensa = 10,
    tipo = "Normal",
    imagen = "rattata.png",
    position = game.at(-10, -10),
    esRival = true
)

const pidgey = new Pokemon(
    nombre = "Pidgey",
    vida = 140,
    vidaMaxima = 140,
    ataque = 30,
    defensa = 12,
    tipo = "Volador",
    imagen = "pidgey.png",
    position = game.at(-10, -10),
    esRival = true
)

const geodude = new Pokemon(
    nombre = "Geodude",
    vida = 230,
    vidaMaxima = 230,
    ataque = 30,
    defensa = 0,
    tipo = "Roca",
    imagen = "geodude.png",
    position = game.at(-10, -10),
    esRival = true
)

const machop = new Pokemon(
    nombre = "Machop",
    vida = 280,
    vidaMaxima = 280,
    ataque = 35,
    defensa = 0,
    tipo = "Lucha",
    imagen = "machop.png",
    position = game.at(-10, -10),
    esRival = true
)

const onix = new Pokemon(
    nombre = "Onix",
    vida = 350,
    vidaMaxima = 350,
    ataque = 45,
    defensa = 5,
    tipo = "Roca",
    imagen = "onix.png",
    position = game.at(-10, -10),
    esRival = true
)

const haunter = new Pokemon(
    nombre = "Haunter",
    vida = 300,
    vidaMaxima = 300,
    ataque = 45,
    defensa = 10,
    tipo = "Fantasma",
    imagen = "haunter.png",
    position = game.at(-10, -10),
    esRival = true
)


// CONTROLADOR DEL JUEGO
object juego {
    const pokemones = [pikachu, charmander, squirtle, bulbasaur]
    const enemigos = [rattata, pidgey, geodude, machop, onix, haunter]
    var property indiceSeleccionado = 0
    var property pokemonElegido = null
    var property pokemonEnemigo = null
    var property enPantallaSeleccion = true
    var property enPantallaInicio = true
    var property seleccionIniciada = false
    var property eligiendoRival = false

    method pokemonActual() = pokemones.get(indiceSeleccionado)

    method moverIzquierda() {
        if (indiceSeleccionado > 0) {
            indiceSeleccionado -= 1
            selector.actualizarPosicion()
            if (enPantallaSeleccion && not eligiendoRival && not combate.enCombate()) {
                game.sound("selection.mp3").play()
            }
        }
    }

    method moverDerecha() {
        if (indiceSeleccionado < pokemones.size() - 1) {
            indiceSeleccionado += 1
            selector.actualizarPosicion()
            if (enPantallaSeleccion && not eligiendoRival && not combate.enCombate()) {
                game.sound("selection.mp3").play()
            }
        }
    }

    method confirmarSeleccion() {
        if (pokemonElegido == null) {  
            pokemonElegido = self.pokemonActual()
            game.schedule(100, { game.say(pokemonElegido, pokemonElegido.nombre() + ", yo te elijo!") })
            if (enPantallaSeleccion && not combate.enCombate()) {
                game.sound("yo_te_elijo.mp3").play()
            }
            // Remover controles de selección para evitar sonidos repetidos
            keyboard.left().onPressDo({})
            keyboard.right().onPressDo({})
            keyboard.enter().onPressDo({})
            // Iniciar selección del rival después de 3 segundos
            game.schedule(3000, { self.iniciarRuletaRival() })
        }
    }

    method iniciarRuletaRival() {
        eligiendoRival = true
        // Limpiar pantalla de selección
        game.removeVisual(pikachu)
        game.removeVisual(charmander)
        game.removeVisual(squirtle)
        game.removeVisual(bulbasaur)
        game.removeVisual(selector)
        game.removeVisual(tituloImagen)
        game.removeVisual(instrucciones)
        game.removeVisual(pikachuStatsImage)
        game.removeVisual(charmanderStatsImage)
        game.removeVisual(squirtleStatsImage)
        game.removeVisual(bulbasaurStatsImage)
        // Agregar visual del rival, título y nombre
        game.addVisual(rivalDisplay)
        game.addVisual(tituloRival)
        game.addVisual(nombreRival)
        // Iniciar efecto de ruleta
        self.efectoRuleta(0)
    }

    method volverAlMenu() {
        // Resetear estados del juego a pantalla de inicio
        pokemonElegido = null
        pokemonEnemigo = null
        enPantallaSeleccion = false
        enPantallaInicio = true
        seleccionIniciada = false
        eligiendoRival = false
        indiceSeleccionado = 0

        // Limpiar visuales y mostrar botón de play
        game.allVisuals().forEach({ visual => game.removeVisual(visual) })
        game.addVisual(playButton)

        // Reasignar control para iniciar selección
        keyboard.enter().onPressDo({ playButton.iniciarSeleccion() })
    }

    method efectoRuleta(contador) {
        if (contador < 15) {  
            const enemigoPorMostrar = enemigos.anyOne()
            rivalDisplay.cambiarPokemon(enemigoPorMostrar)
            game.sound("selection.mp3").play()
            nombreRival.cambiarPokemon(enemigoPorMostrar)
            game.schedule(100, { self.efectoRuleta(contador + 1) })
        } else {
            // Elegir enemigo final
            pokemonEnemigo = enemigos.anyOne()
            rivalDisplay.cambiarPokemon(pokemonEnemigo)
            game.sound("selection.mp3").play()
            nombreRival.cambiarPokemon(pokemonEnemigo)
            eligiendoRival = false
            game.schedule(2000, { combate.iniciar(pokemonElegido, pokemonEnemigo) })
        }
    }
}


// Marco seleccion
object selector {
    var property position = game.at(1, 10)

    method actualizarPosicion() {
        const pokemon = juego.pokemonActual()
        position = game.at(pokemon.position().x() - 0.5, pokemon.position().y() - 0.5)
    }

    method image() = "selector.png"
}

// Display del rival (para efecto ruleta)
object rivalDisplay {
    var property position = game.at(17, 10)
    var pokemonActual = rattata  

    method cambiarPokemon(pokemon) {
        pokemonActual = pokemon
    }

    method image() = pokemonActual.image()
}

// Título para pantalla del rival
object tituloRival {
    method position() = game.at(13.5, 17)
    method image() = "titulo_rival.png"
    method scale() = 0.10
}

// Nombre del rival
object nombreRival {
    var pokemonActual = rattata

    method position() = game.at(17, 6)

    method cambiarPokemon(pokemon) {
        pokemonActual = pokemon
    }

    method image() = "nombre_" + pokemonActual.nombre().toLowerCase() + ".png"
}


// INTERFAZ DE SELECCIÓN
object tituloImagen {
    method position() = game.at(13.5, 17)
    method image() = "titulo.png"
    method scale() = 0.10
}

object instrucciones {
    method position() = game.at(4, 1)
    method text() = "← → elegir | ENTER confirmar"
    method textColor() = "FFFFFFCC"
}

// PIKACHU
object pikachuStatsImage {
    method position() = game.at(3, 3)
    method image() = "pikachu_stats.png"
    method scale() = 0.8
}

// CHARMANDER
object charmanderStatsImage {
    method position() = game.at(10.5, 3)
    method image() = "charmander_stats.png"
    method scale() = 0.8
}

// SQUIRTLE
object squirtleStatsImage {
    method position() = game.at(18.5, 3)
    method image() = "squirtle_stats.png"
    method scale() = 0.8
}

// BULBASAUR
object bulbasaurStatsImage {
    method position() = game.at(27, 3)
    method image() = "bulbasaur_stats.png"
    method scale() = 0.8
}

// BOTÓN PLAY
object playButton {
    method position() = game.at(15, 6)
    method image() = "play.png"
    method scale() = 0.5

    method iniciarSeleccion() {
        if (not juego.seleccionIniciada()) {
            if (juego.enPantallaInicio()) {
                game.sound("choose_your_pokemon.mp3").play()
            }
            juego.seleccionIniciada(true)
            juego.enPantallaInicio(false)
            // Posicionar Pokémon
            pikachu.position(game.at(5, 11))
            charmander.position(game.at(13, 11))
            squirtle.position(game.at(21, 11))
            bulbasaur.position(game.at(29, 11))
            // Agregar selección
            game.addVisual(pikachu)
            game.addVisual(charmander)
            game.addVisual(squirtle)
            game.addVisual(bulbasaur)
            game.addVisual(selector)
            selector.actualizarPosicion()
            game.addVisual(tituloImagen)
            game.addVisual(instrucciones)
            game.addVisual(pikachuStatsImage)
            game.addVisual(charmanderStatsImage)
            game.addVisual(squirtleStatsImage)
            game.addVisual(bulbasaurStatsImage)
            // Limpiar controles anteriores
            keyboard.enter().onPressDo({})
            // Controles
            keyboard.left().onPressDo({ juego.moverIzquierda() })
            keyboard.right().onPressDo({ juego.moverDerecha() })
            keyboard.enter().onPressDo({ juego.confirmarSeleccion() })
            // Remover botón
            game.removeVisual(self)
        }
    }
}


// PROGRAMA PRINCIPAL
program juegoInicio {
    // Configuración del juego
    game.title("Gimnasio Pokémon")
    game.width(40)
    game.height(20)
    game.cellSize(64)
    game.boardGround("fondo_pantalla.png")

    // Agregar botón play inicialmente
    game.addVisual(playButton)

    // Control para iniciar selección con ENTER
    keyboard.enter().onPressDo({ playButton.iniciarSeleccion() })

    // Iniciar el juego
    game.start()
}
